E000R
10 PRINT:PRINT:PRINT "SUM OF TWO LARGE WHOLE POSITIVE NUMBERS":PRINT
12 PRINT "BASED ON SUM OF TWO NUMBERS FROM GEEKSFORGEEKS.ORG"
14 PRINT "REWRITTEN FOR APPLE ONE BASIC BY @BRIAN-ENDERS":PRINT
20 DIM A$(128), B$(128), Z$(128), X$(128), N$(10)
30 N$ = "1234567890"
40 INPUT "ENTER THE FIRST NUMBER", A$
50 L = LEN(A$)
60 FOR I = 1 TO L
70 FOR O = 1 TO 10
80 IF A$(I,I) = N$(O,O) THEN 110
90 NEXT O
100 GOTO 40
110 NEXT I
120 INPUT "ENTER THE SECOND NUMBER", B$
130 L = LEN(B$)
140 FOR I = 1 TO L
150 FOR O = 1 TO 10
160 IF B$(I,I) = N$(O,O) THEN 190
170 NEXT O
180 GOTO 120
190 NEXT I
200 IF LEN(A$) < LEN(B$) THEN 220
210 Z$=A$:A$=B$:B$=Z$
220 PRINT B$:TAB LEN(B$)-LEN(A$)+1:PRINT A$
230 L1 = LEN(A$): L2 = LEN(B$)
240 X$ = A$:GOSUB 2000:A$ = X$
250 X$ = B$:GOSUB 2000:B$ = X$
260 C = 0
270 FOR I = 1 TO L1
280 A = 0: B = 0
290 FOR O = 1 TO 9
300 IF A$(I,I) = N$(O,O) THEN 320
310 NEXT O
315 A = 0:GOTO 330
320 A = O
330 FOR O = 1 TO 9
340 IF B$(I,I) = N$(O,O) THEN 360
350 NEXT O
355 B = 0:GOTO 370
360 B = O
370 S1 = A+B+C
380 S2 = S1 MOD 10
381 IF S2 <> 0 THEN 390
382 S2 = 10
390 Z$(I) = N$(S2, S2)
400 C = S1/10
410 NEXT I
415 IF L1 = L2 THEN 530
420 FOR I = L1+1 TO L2
430 B = 0
440 FOR O = 1 TO 9
450 IF B$(I,I) = N$(O,O) THEN 470
460 NEXT O
465 B = 0:GOTO 480
470 B = O
480 S1 = B+C
490 S2 = S1 MOD 10
491 IF S2 <> 0 THEN 500
492 S2 = 10
500 Z$(I) = N$(S2, S2)
510 C = S1/10
520 NEXT I
530 IF C = 0 THEN 550
540 Z$(L2+1) = N$(C,C)
550 X$ = Z$:GOSUB 2000:Z$ = X$
560 PRINT Z$
1000 END
2000 REM REVERSE STRING X
2001 Z = LEN(X$)
2002 FOR I = 1 TO Z
2003 Z$(I) = X$(Z+1-I,Z+1-I)
2004 NEXT I
2005 X$ = Z$
2006 RETURN